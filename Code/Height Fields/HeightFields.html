<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8"> 

<title>Load Model</title>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="../Common/Trackball.js"></script>
<script type="text/javascript" src="../Common/Camera.js"></script>
<script type="text/javascript" src="../Common/Object.js"></script>
<script type="text/javascript" src="../Common/Utils.js"></script>

<script type="text/javascript" src="HeightFields.js"></script> 

<script id="vertex-shader" type="x-shader/x-vertex">
precision highp float;


attribute vec4 vPosition;
attribute vec3 vNormal;

uniform mat4 M, TB, VP;
uniform mat3 N, TBN;
uniform vec3 Ka, Kd, Ks, Ia, Id, Is, lightPosition, cameraPosition;
uniform float shininess;
uniform float time;

varying vec3 fNormal, fPosition;

float sinc(float x){
    if(x==0.0) return 1.0;
    return sin(x)/x;
}

float sumOfSines(float x, float y, float t){
	float s = 0.0;
	s += 0.3*sin(4.0*x + 2.0*y + 2.0*t);
	s+= -0.3*sin(6.0*x + 0.6*y - 1.2*t);
	s+= -0.2*sin(4.0*y - 3.1*x + 0.7*t);
	s+= 0.1*sin(8.0*x - 5.0*y - t);
	return 0.1*s;
}

float f(float x, float y, float t){
    float r = sqrt(x*x + y*y);
    //return sinc(15.0*r);
    //return sinc(30.0*r)*sin(r-0.5*t);
    return sumOfSines(x,y,t);
}


vec3 computeNormal(float x,float y, float t){
	float delta = 0.01;
	float dfdx = (f(x+delta, y,t) - f(x,y,t))/delta;
	float dfdy = (f(x, y+delta, t) - f(x,y,t))/delta;
	return vec3(-dfdx,-dfdy, 1.0);
}

void main(){
	int i;


	vec4 v = vPosition;
	v.z = f(v.x,v.y,time);

	vec3 vn = vNormal; // useless line just so that the optimizer doesn't remove vNormal.

	vec3 n = computeNormal(v.x, v.y, time);

	vec4 wPos = TB*M*v; // world position
	
	v = VP*wPos;   
	v.z = -v.z;  // compensate for left handed coordinate system used by WebGL
	gl_Position = v;

	fPosition = wPos.xyz;
	fNormal = TBN*N*n;
}


</script>

<script id="fragment-shader" type="x-shader/x-fragment">
precision highp float;

uniform vec3 Ka, Kd, Ks, Ia, Id, Is, lightPosition, cameraPosition;
uniform float shininess;

varying vec4 fColor;
varying vec3 fPosition, fNormal;

vec4 computeColor(vec3 position, vec3 normal) {
	// we are doing lighting in world coordinate frame

	vec3 lightDir = normalize(lightPosition - position);
	vec3 viewDir = normalize(cameraPosition - position);

	vec3 ambient = Ia*Ka ;
	vec3 diffuse = Id*Kd* max(0.0, dot(normal, lightDir));

	vec3 halfVector = normalize(lightDir + viewDir);
	vec3 specular = Is*Ks* pow( max(dot(halfVector, normal), 0.0), shininess);

	vec3 color = ambient + diffuse + specular;

	return vec4(color, 1.0);
}

void main(){
		gl_FragColor = computeColor(fPosition, normalize(fNormal));
}
</script>
</head>
<body>
   <canvas id="gl-canvas" width="512" height="512">
    HTML5 Canvas not supported!
   </canvas>
</body>
</html>

