<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8"> 

<title>Load Model</title>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="../Common/Trackball.js"></script>
<script type="text/javascript" src="../Common/Camera.js"></script>
<script type="text/javascript" src="../Common/Object.js"></script>
<script type="text/javascript" src="../Common/Utils.js"></script>

<script type="text/javascript" src="NormalMapping.js"></script> 


<script id="vertex-shader" type="x-shader/x-vertex">
precision highp float;

attribute vec4 vPosition;
attribute vec3 vNormal,vTangent,vBitangent;
attribute vec2 vTexCoord;

uniform mat4 M, TB, VP;

varying vec3 fNormal, fPosition, fTangent, fBitangent;
varying vec2 fTexCoord;

void main(){

	vec4 wPos = TB*M*vPosition; // world position

	vec4 v = VP*wPos;   
	v.z = -v.z; // compensate for left handed coordinate system used by WebGL
	gl_Position = v;

	fPosition = wPos.xyz;

	fNormal = vNormal;
	fTangent = vTangent;
	fBitangent = vBitangent;

	fTexCoord = vTexCoord;
}


</script>

<script id="fragment-shader" type="x-shader/x-fragment">
precision highp float;

uniform vec3 Ka, Kd, Ks, Ia, Id, Is, lightPosition, cameraPosition;
uniform float shininess;
uniform int usingDiffuseMap, usingNormalMap;
uniform sampler2D diffuseMapSampler, normalMapSampler;
uniform mat3 N, TBN;

varying vec4 fColor;
varying vec3 fNormal, fPosition, fTangent, fBitangent;
varying vec2 fTexCoord;
vec3 kd;

vec4 computeColor(vec3 position, vec3 normal) {
	// we are doing lighting in world coordinate frame

	vec3 lightDir = normalize(lightPosition - position);
	vec3 viewDir = normalize(cameraPosition - position);

	vec3 ambient = Ia*Ka ;
	
	vec3 diffuse = Id*kd* max(0.0, dot(normal, lightDir));
	
	vec3 halfVector = normalize(lightDir + viewDir);
	vec3 specular = Is*Ks* pow( max(dot(halfVector, normal), 0.0), shininess);

	vec3 color = ambient + diffuse + specular;

	return vec4(color, 1.0);
}

void main(){
	vec3 normal;

	if(usingDiffuseMap == 1){
		kd = texture2D(diffuseMapSampler, fTexCoord).rgb;
	}
	else{
		kd = Kd;
	}

	if(usingNormalMap == 1){
		vec3 c = texture2D(normalMapSampler, fTexCoord).rgb;
		c = 2.0*c - vec3(1.0,1.0,1.0);
		normal = c.r*fTangent + c.g*fBitangent+ c.b*fNormal;
	}
	else{
		normal = fNormal;
	}

	normal = normalize(TBN*N*normal);

	gl_FragColor = computeColor(fPosition, normal);
}
</script>
</head>
<body>
   <canvas id="gl-canvas" width="512" height="512">
    HTML5 Canvas not supported!
   </canvas>
</body>
</html>

